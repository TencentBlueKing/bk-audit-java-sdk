plugins {
    id "maven-publish"
    id "signing"
}

description = 'BlueKing Audit Java SDK'

dependencies {
    api("com.fasterxml.jackson.module:jackson-module-jsonSchema")
    implementation("com.fasterxml.jackson.core:jackson-core")
    implementation("com.fasterxml.jackson.core:jackson-databind")
    implementation("com.fasterxml.jackson.core:jackson-annotations")
    implementation("org.slf4j:slf4j-api")
    implementation("org.apache.commons:commons-lang3")
    implementation("commons-collections:commons-collections")
    implementation("org.apache.commons:commons-collections4")
    implementation("jakarta.servlet:jakarta.servlet-api")
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.assertj:assertj-core")
    testImplementation("org.mockito:mockito-core")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    compileOnly("org.projectlombok:lombok")
    testCompileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")
    testAnnotationProcessor("org.projectlombok:lombok")
}
publishing {
    publications {
        mavenJava (MavenPublication) {
            groupId = 'com.tencent.bk.sdk'
            version = project.property('version')
            artifactId = 'bk-audit-java-sdk'

            from components.java

            artifact sourceJar
            artifact javadocJar

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'bk-audit-java-sdk-core'
                description = 'Tencent BlueKing BK-Audit Java SDK core'
                url = 'https://github.com/TencentBlueKing/bk-audit-java-sdk'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/TencentBlueKing/bk-audit-java-sdk/blob/master/License.txt'
                    }
                }
                developers {
                    developer {
                        id = 'bk-job'
                        name = 'bk-job'
                        email = 'bk-job@tencent.com'
                        roles = ['Manager']
                    }
                }
                scm {
                    url = 'https://github.com/TencentBlueKing/bk-audit-java-sdk'
                    connection = 'scm:git:https://github.com/TencentBlueKing/bk-audit-java-sdk.git'
                    developerConnection = 'scm:git:git@github.com:TencentBlueKing/bk-audit-java-sdk.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

publish.onlyIf { !sourceSets.main.allSource.files.isEmpty() }
generateMetadataFileForMavenJavaPublication.onlyIf { !sourceSets.main.allSource.files.isEmpty() }
generatePomFileForMavenJavaPublication.onlyIf { !sourceSets.main.allSource.files.isEmpty() }
publishMavenJavaPublicationToMavenLocal.onlyIf { !sourceSets.main.allSource.files.isEmpty() }
publishToMavenLocal.onlyIf { !sourceSets.main.allSource.files.isEmpty() }
signMavenJavaPublication.onlyIf { !sourceSets.main.allSource.files.isEmpty() }


