plugins {
    id "io.spring.dependency-management" version "1.1.0"
    id "org.springframework.boot" version "2.6.13" apply false
    id "java-library"
    id "idea"
    id "maven-publish"
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
    id 'com.dorongold.task-tree' version '2.0.1'
}

description = "Blueking Audit Java SDK Build"

ext {
    springVersion = "5.3.25"
    springBootVersion = "2.6.13"
    aspectjVersion = "1.9.7"
    jacksonVersion = "2.13.4"
    slf4jVersion = "1.7.30"
    commonsLang3Version = "3.12.0"
    commonsCollectionsVersion = "3.2.2"
    commonsCollections4Version = "4.4"
    servletVersion = "4.0.1"
    junitVersion = "5.8.2"
    assertjVersion = "3.21.0"
    lombokVersion = "1.18.10"
    mockitoVersion = "4.0.0"
    hamcrestVersion = "2.2"
}

allprojects {

    group = "com.tencent.bk.sdk"
    version = project.property("version")

    apply plugin: "idea"
    apply plugin: "eclipse"
    apply plugin: "java-library"
    apply plugin: "io.spring.dependency-management"

    // 版本管理
    dependencyManagement {
        dependencies {
            dependency("org.springframework.boot:spring-boot-autoconfigure:$springBootVersion")
            dependency("org.springframework.boot:spring-boot-test-autoconfigure:$springBootVersion")
            dependency("org.springframework.boot:spring-boot-configuration-processor:$springBootVersion")
            dependency("org.springframework:spring-web:$springVersion")
            dependency("org.springframework:spring-webmvc:$springVersion")
            dependency("org.springframework:spring-test:$springVersion")
            dependency("org.springframework.boot:spring-boot-test:$springBootVersion")
            dependencySet(group: "org.aspectj", version: "$aspectjVersion") {
                entry "aspectjrt"
                entry "aspectjweaver"
            }
            dependencySet(group: "com.fasterxml.jackson.core", version: "$jacksonVersion") {
                entry "jackson-core"
                entry "jackson-databind"
                entry "jackson-annotations"
            }
            dependency("com.fasterxml.jackson.module:jackson-module-jsonSchema:$jacksonVersion")
            dependency("org.slf4j:slf4j-api:$slf4jVersion")
            dependency("org.apache.commons:commons-lang3:$commonsLang3Version")
            dependency("commons-collections:commons-collections:$commonsCollectionsVersion")
            dependency("org.apache.commons:commons-collections4:$commonsCollections4Version")
            dependency("javax.servlet:javax.servlet-api:$servletVersion")
            dependencySet(group: "org.junit.jupiter", version: "$junitVersion") {
                entry "junit-jupiter-api"
                entry "junit-jupiter-engine"
            }
            dependency("org.assertj:assertj-core:$assertjVersion")
            dependency("org.projectlombok:lombok:$lombokVersion")
            dependency("org.mockito:mockito-core:$mockitoVersion")
            dependency("org.hamcrest:hamcrest:$hamcrestVersion")
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
        }
        include '**/*Test.class'
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
    tasks.withType(Javadoc) {
        options.encoding("UTF-8")
    }

    task sourceJar(type: Jar) {
        classifier "sources"
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier "javadoc"
        from javadoc.destinationDir
    }

    javadoc {
        description = "javadoc for bk-auit-java-sdk"
        options.memberLevel = JavadocMemberLevel.PROTECTED
        options.author = true
        options.version = true
        options.header = project.name
        // 不检查：非标的javadoc注解不报错
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption("charset", "UTF-8")
        logging.captureStandardError LogLevel.INFO
        logging.captureStandardOutput LogLevel.INFO
        options.encoding = "UTF-8"
        options.charSet = "UTF-8"
    }
}

nexusPublishing {
    repositories {
        sonatype {
            // 正式包
            def mavenRepoDeployUrl = System.getProperty("mavenRepoDeployUrl")
            def mavenRepoUsername = System.getProperty("mavenRepoUsername")
            def mavenRepoPassword = System.getProperty("mavenRepoPassword")

            if (mavenRepoDeployUrl == null || mavenRepoDeployUrl.trim().length() == 0) {
                // 从环境变量读取
                mavenRepoDeployUrl = System.getenv("MAVEN_REPO_DEPLOY_URL")
                mavenRepoUsername = System.getenv("MAVEN_REPO_USERNAME")
                mavenRepoPassword = System.getenv("MAVEN_REPO_PASSWORD")
            }

            if (mavenRepoDeployUrl == null || mavenRepoDeployUrl.trim().length() == 0) {
                // gradle.properties 中的 maven deploy repo
                mavenRepoDeployUrl = project.findProperty("MAVEN_REPO_DEPLOY_URL")
                mavenRepoUsername = project.findProperty("MAVEN_REPO_USERNAME")
                mavenRepoPassword = project.findProperty("MAVEN_REPO_PASSWORD")
            }

            // 快照包
            def snapshotMavenRepoDeployUrl = System.getProperty("snapshotMavenRepoDeployUrl")
            def snapshotMavenRepoUsername = System.getProperty("snapshotMavenRepoUsername")
            def snapshotMavenRepoPassword = System.getProperty("snapshotMavenRepoPassword")

            if (snapshotMavenRepoDeployUrl == null || snapshotMavenRepoDeployUrl.trim().length() == 0) {
                // gradle.properties 中的 snapshot maven deploy repo
                snapshotMavenRepoDeployUrl = project.findProperty("SNAPSHOT_MAVEN_REPO_DEPLOY_URL")
                snapshotMavenRepoUsername = project.findProperty("SNAPSHOT_MAVEN_REPO_USERNAME")
                snapshotMavenRepoPassword = project.findProperty("SNAPSHOT_MAVEN_REPO_PASSWORD")
            }

            def isSnapshot = project.property('version').toString().endsWith("SNAPSHOT")

            def url = isSnapshot ? snapshotMavenRepoDeployUrl : mavenRepoDeployUrl
            println("Publish to url:" + url)

            nexusUrl = uri(mavenRepoDeployUrl == null ? "https://ossrh-staging-api.central.sonatype.com/service/local/" : mavenRepoDeployUrl)
            snapshotRepositoryUrl = uri(snapshotMavenRepoDeployUrl == null ? "https://central.sonatype.com/repository/maven-snapshots/" : snapshotMavenRepoDeployUrl)

            username = isSnapshot ? snapshotMavenRepoUsername : mavenRepoUsername
            password = isSnapshot ? snapshotMavenRepoPassword : mavenRepoPassword
        }
    }
}
